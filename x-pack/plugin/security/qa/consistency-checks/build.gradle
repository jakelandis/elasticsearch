apply plugin: 'elasticsearch.standalone-test'

//since we are placing everything on the classpath for this test we need to force versions to converge or simply ignore them
configurations.all {
  resolutionStrategy {
    force "org.slf4j:slf4j-api:2.0.10"
    force "commons-codec:commons-codec:1.16.1"
    force "org.slf4j:slf4j-nop:2.0.10"
    force "org.apache.commons:commons-lang3:3.14.0"
    force "com.fasterxml.jackson.core:jackson-databind:2.15.0"
    force "com.fasterxml.jackson.core:jackson-core:2.15.4"
    force "com.fasterxml.jackson.core:jackson-annotations:2.15.4"
    force "joda-time:joda-time:2.10.14"
    force "org.ow2.asm:asm:8.0.1"
    force "com.sun.mail:jakarta.mail:1.6.4"

    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'jakarta.xml.bind', module: 'jakarta.xml.bind-api'
    exclude group: 'jakarta.activation', module: 'jakarta.activation-api'
  }
}


dependencies {
  testImplementation "org.reflections:reflections:0.10.2"
  testImplementation "org.javassist:javassist:3.30.2-GA"
  testImplementation(testArtifact(project(xpackModule('core'))))

  project.rootProject.subprojects.findAll { it.parent.path == ':modules' }.each { Project module ->
    testImplementation module
  }

  Project xpack = project(':x-pack:plugin')
  xpack.subprojects.findAll { it.parent == xpack }.each { Project xpackModule ->
    testImplementation xpackModule
  }

}

tasks.named("test").configure {
  systemProperty 'tests.security.manager', 'false'
  //TODO: add system property for registry debug
}
